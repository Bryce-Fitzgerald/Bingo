apply plugin: 'com.android.application'

Properties prop = new Properties()
prop.load(new FileInputStream(file("signing.properties")))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.sun.bingo"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 13
        versionName "1.5.0"
        manifestPlaceholders = [umengHolder: "bingo"]
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        release {
            storeFile file(prop['KEYSTORE_FILEPATH'])
            storePassword prop['STORE_PASSWORD']
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            //压缩资源
            shrinkResources true
            //zip优化
            zipAlignEnabled true
            //apk签名
            signingConfig signingConfigs.release
            //release版
            buildConfigField "boolean", "LOG_DEBUG", "true"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateAppName(variant)
            }
        }

        debug{
            //压缩资源
            shrinkResources false
            //zip优化
            zipAlignEnabled false
            //apk签名
            signingConfig signingConfigs.release
            //debug版
            buildConfigField "boolean", "LOG_DEBUG", "false"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all {
                variant -> generateAppName(variant)
            }
        }
    }

}

//生成自定义App名称
def generateAppName(variant) {
    variant.outputs.each {
        output -> output.outputFile = new File (output.outputFile.parent,
            "Bingo_${variant.buildType.name}_${variant.versionName}.apk".toLowerCase())
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:gridlayout-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'

    // DroidFramework
    compile project(':DroidFramework')

    // ButterKnife
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    // SharedPreferences
    compile 'de.devland.esperandro:esperandro-api:2.2.0'
    annotationProcessor 'de.devland.esperandro:esperandro:2.2.0'

    // 友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'

    // SystemBarTint
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'

    compile 'com.nineoldandroids:library:2.4.0'
    compile 'cn.bingoogolapple:bga-refreshlayout:1.0.4@aar'

    // Floating Action Button
    compile 'com.shamanland:fab:0.0.8'

    // RichEditor
    compile 'jp.wasabeef:richeditor-android:0.1'

    // PhotoView
    compile 'com.github.chrisbanes.photoview:library:1.2.4'

    // LoadingView
    compile 'com.github.zzz40500:android-shapeLoadingView:1.0.3.2'

    // EventBus
    compile 'org.greenrobot:eventbus:3.0.0'

    // Glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.koushikdutta.urlimageviewhelper:urlimageviewhelper:1.0.4'
    compile 'org.jsoup:jsoup:1.8.3'

}
